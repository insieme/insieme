
using "ext.datapath";

let int = int<4>;

let pair = struct {
	int first;
	int second;
};

let obj = struct {
	int a;
	array<int,4> b;
	pair c;
};

unit main() {

	decl ref<obj> o;
	
	// check element o.a
	o.a = 0;
	print("o.a = %d\n", *o.a);
	
	// updated member a directly
	decl ref<int> x = ref_narrow(o, dp_member(dp_root(type(obj)), lit("a"), type(int)));
	x = 12;
	print("Equal: %d\n", ref_eq(o.a, x));
	print("x = %d \t o.a = %d\n", *x, *o.a);

	// udpate element of b directly
	o.b[2u] = 0;
	print("o.b[2] = %d\n", *o.b[2u]);
	
	decl ref<int> y = ref_narrow(o, dp_element(dp_member(dp_root(type(obj)), lit("b"), type(array<int,4>)), 2u));
	y = 12;
	print("Equal: %d\n", ref_eq(o.b[2u], y));
	print("y = %d \t o.b[2] = %d\n", *y, *o.b[2u]);
	
	
	// expand a definition
	decl ref<array<int,4>> v = ref_expand(y, dp_element(dp_root(type(array<int,4>)), 2u));
	v[1u] = 10;
	v[2u] = 14;
	
	print("Equal: %d\n", ref_eq(o.b, v));
	print("v[1] = %d \t v[2] = %d \t o.v[1] = %d \t o.v[2] = %d \t y = %d\n",
			*v[1u], *v[2u], *o.b[1u], *o.b[2u], *y);
	
	
	// handle nested element
	decl ref<int> first = ref_narrow(o, dp_member(dp_member(dp_root(type(obj)), lit("c"), type(pair)), lit("first"), type(int)));
	
	// check reference equality
	print("Equal: %d\n", ref_eq(o.c.first, first));
	
	// and the reverse
	decl ref<obj> full = ref_expand(first, dp_member(dp_member(dp_root(type(obj)), lit("c"), type(pair)), lit("first"), type(int)));
	print("Equal: %d\n", ref_eq(o,full));
	
}
